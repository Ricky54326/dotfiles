
# Load dependencies.
pmodload 'helper'

function prompt_riccardo_pwd {
  local pwd="${PWD/#$HOME/~}"
  _prompt_riccardo_pwd="$pwd"
}

function prompt_riccardo_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_riccardo_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

}

function prompt_riccardo_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for icalling git-info before each command.
  add-zsh-hook precmd prompt_riccardo_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  #zstyle ':prezto:module:editor:info:keymap:primary' format '❯'
  zstyle ':prezto:module:editor:info:keymap:primary' format '»'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ''
  zstyle ':prezto:module:git:info:added' format '✚'
  zstyle ':prezto:module:git:info:ahead' format '%F{yellow}↑%f%F{248}(%F{yellow}%A%f%F{248})'
  zstyle ':prezto:module:git:info:behind' format '↓(%B)'
  zstyle ':prezto:module:git:info:branch' format '%F{68}%b%f%F{248}'
  zstyle ':prezto:module:git:info:clean' format '%F{green}✓%f%F{248}'
  zstyle ':prezto:module:git:info:dirty' format  '%F{red}⚡%f%F{248}(%F{red}%D%f%F{248})'
  zstyle ':prezto:module:git:info:commit' format '➦ %.7c'
  zstyle ':prezto:module:git:info:deleted' format '✖'
  zstyle ':prezto:module:git:info:modified' format '✱'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:renamed' format '➜'
  zstyle ':prezto:module:git:info:stashed' format 's'
  zstyle ':prezto:module:git:info:unmerged' format '═'
  zstyle ':prezto:module:git:info:untracked' format '?'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' [$(coalesce "%b" "%p" "%c")%s $(coalesce "%C" "%D" "") $(coalesce "%A" "%B" "\b")]' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='%F{119}${_prompt_riccardo_pwd}%f%F{248}${git_info:+${(e)git_info[prompt]}}%(!. %B%F{red}#%f%b.)%f
%F{119}${editor_info[keymap]}%f '
  RPROMPT='%(?..%F{red}%? ↵%f)'
}

prompt_riccardo_setup "$@"

